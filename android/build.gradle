
buildscript {
    def kotlin_version = rootProject.ext.has('RNHV_kotlinVersion') ? rootProject.ext.get('RNHV_kotlinVersion') : project.properties['RNHV_kotlinVersion']
    def gradle_version = rootProject.ext.has('RNHV_gradleVersion') ? rootProject.ext.get('RNHV_gradleVersion') : project.properties['RNHV_gradleVersion']

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$gradle_version"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'

def safeExtGet(prop, fallback) {
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
}

android {
    compileSdkVersion safeExtGet('compileSdkVersion', 30)
    buildToolsVersion safeExtGet('buildToolsVersion', '31.0.0')

    defaultConfig {
        minSdkVersion safeExtGet('minSdkVersion', 23)
        targetSdkVersion safeExtGet('targetSdkVersion', 30)
        versionCode 1
        versionName "1.0.0"
    }
    lintOptions {
        abortOnError false
    }
}

repositories {
    google()
    jcenter()
    mavenCentral()
    maven {
        // All of React Native (JS, Obj-C sources, Android binaries) is installed from npm
        url "$rootDir/../../../node_modules/react-native/android"
    }
}

def kotlin_version = safeExtGet('kotlinVersion', project.properties['RNHV_kotlinVersion'])

dependencies {
    implementation 'com.facebook.react:react-native:+'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}
